plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'org.jlleitschuh.gradle.ktlint'
}

android {
    compileSdkVersion buildVersions.compileSdkVersion
    buildToolsVersion buildVersions.buildToolsVersion

    defaultConfig {
        applicationId "m4trixtm.sublite"
        minSdkVersion buildVersions.minSdkVersion
        targetSdkVersion buildVersions.targetSdkVersion
        versionCode buildVersions.versionCode
        versionName buildVersions.versionName

        buildConfigField("String", "SUB_API_KEY", getApiKey())

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        androidTest.java.srcDirs += "src/test-common/java"
        test.java.srcDirs += "src/test-common/java"
        test.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    testOptions {
        animationsDisabled = true
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude("META-INF/*.kotlin_module")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

String getApiKey() {
    def propFile = rootProject.file("./local.properties")
    def properties = new Properties()
    properties.load(new FileInputStream(propFile))
    return properties['SUB_API_KEY']
}

dependencies {

    implementation libraries.stdLib
    implementation libraries.coroutines
    implementation libraries.coroutinesCore
    implementation libraries.coreKTX
    implementation libraries.appCompat
    implementation libraries.material
    implementation libraries.constraintLayout
    implementation libraries.legacySupport

    //arch components
    implementation libraries.viewModel
    implementation libraries.fragment
    implementation libraries.viewModelSavedState
    implementation libraries.commonJava8

    implementation libraries.navigationFragment
    implementation libraries.navigationUI

    //retrofit
    implementation libraries.retrofit
    implementation libraries.retrofitGson
    implementation libraries.networkResponseAdapter

    //room
    implementation libraries.room
    implementation libraries.roomKtx
    kapt libraries.roomCompiler
    testImplementation libraries.roomTesting

    //hilt
    implementation libraries.hiltAndroid
    kapt libraries.hiltCompiler
    implementation libraries.hiltLifeCycle
    kapt libraries.hiltLifeCycleCompiler

    //coil
    implementation libraries.coil

    //lottie
    implementation libraries.lottie

    //leakCanary
    debugImplementation libraries.leakCanary

    //whatIf
    implementation libraries.whatIf

    //bundler
    implementation libraries.bundler

    testImplementation testLibraries.jUnit
    testImplementation testLibraries.coroutinesTest
    testImplementation testLibraries.coroutinesAndroidTest
    testImplementation testLibraries.robolectric
    testImplementation testLibraries.coreTesting
    testImplementation testLibraries.mockWebServer
    testImplementation testLibraries.mockitoKotlin
    testImplementation testLibraries.googleTruth
    androidTestImplementation testLibraries.androidJUnit
    androidTestImplementation testLibraries.espressoCore
}

kapt {
    correctErrorTypes true
}